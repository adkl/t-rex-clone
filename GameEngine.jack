class GameEngine {
    field Array enemySequence;
    field int enemySequenceLength;

    field int screenLength;
    field int screenHeight;
    field int bitmapBoxSize;

    field int dinoXPositionOffset;
    field int dinoNoJumpYPosition;
    field int dinoJumpYPosition;

    constructor GameEngine new() {
        return this;
    }

    method void initializeComponents() {
        do initializeEnemySequence();

        let screenHeight = 256;
        let screenLength = 512;
        let bitmapBoxSize = 16;

        let enemySequenceLength = 32;

        let dinoXPositionOffset = ((screenLength / bitmapBoxSize) / 2) - 1; // half of horizontal axis
        let dinoNoJumpYPosition = (screenHeight / bitmapBoxSize) * 11 * 32; // 10th row
        let dinoJumpYPosition = (screenHeight / bitmapBoxSize) * 10 * 32; // 11th row

        return;
    }

    method void run() {
        var char key;
        var int currentEnemy;
        var int dinoPosition;
        var boolean clash;
        var int gameStep;
        var int currentEnemyIndex;
        var boolean jumped;
        var int jumpCountdown;
        var boolean dinoJustLanded;
        var boolean dinoJustJumped;

        let clash = false;

        let gameStep = 0;

        do initializeComponents();
        do drawTerrain();

        while(~clash) {
            let currentEnemyIndex = MathUtils.getCircularIndex(enemySequenceLength, MathUtils.modulo(32 + gameStep, 32), dinoXPositionOffset + 1);
            
            if (gameStep < 15) {
                let currentEnemy = 0;
            }
            else {
                let currentEnemy = enemySequence[currentEnemyIndex];
            }

            if (jumped & (jumpCountdown > 0)) {
                let jumpCountdown = jumpCountdown - 1;
            }

            if (jumpCountdown = 0) {
                let jumped = false;
                let dinoJustLanded = true;
            }

            let key = Keyboard.keyPressed();
            if ((key = 131) & ~jumped) { // up arrow
                let jumped = true;
                let jumpCountdown = 4;
                let dinoJustJumped = true;
            }

            if (jumped) {
                let dinoPosition = dinoJumpYPosition + dinoXPositionOffset;
            }
            else {
                let dinoPosition = dinoNoJumpYPosition + dinoXPositionOffset;
            }

            if (dinoJustLanded) {
                do Graphics.drawBlank(dinoJumpYPosition + dinoXPositionOffset);
            }

            if (dinoJustJumped) {
                do Graphics.drawBlank(dinoNoJumpYPosition + dinoXPositionOffset);
            }

            do Graphics.drawDino(dinoPosition);
            do printEnemySequence(gameStep, dinoPosition);

            if ((currentEnemy = 1) & ~jumped) {
                let clash = true;
            }
            
            // prepare next stage
            let dinoJustLanded = false;
            let dinoJustJumped = false;

            let gameStep = gameStep + 1;
            do Sys.wait(400);
        }

        return;
        // clash happened
    }

    method void initializeEnemySequence() {
        let enemySequence = Array.new(32);
        let enemySequence[0] = 1; // cactus
        let enemySequence[1] = 0;
        let enemySequence[2] = 0;
        let enemySequence[3] = 0;
        let enemySequence[4] = 0;
        let enemySequence[5] = 0;
        let enemySequence[6] = 0;
        let enemySequence[7] = 1;
        let enemySequence[8] = 0;
        let enemySequence[9] = 0;
        let enemySequence[10] = 0;
        let enemySequence[11] = 0;
        let enemySequence[12] = 0;
        let enemySequence[13] = 0;
        let enemySequence[14] = 1;
        let enemySequence[15] = 0;
        let enemySequence[16] = 0;
        let enemySequence[17] = 0;
        let enemySequence[18] = 0;
        let enemySequence[19] = 0;
        let enemySequence[20] = 0;
        let enemySequence[21] = 0;
        let enemySequence[22] = 0;
        let enemySequence[23] = 0;
        let enemySequence[24] = 1; // cactus
        let enemySequence[25] = 0;
        let enemySequence[26] = 0;
        let enemySequence[27] = 0;
        let enemySequence[28] = 0;
        let enemySequence[29] = 0;
        let enemySequence[30] = 0;
        let enemySequence[31] = 0;
        return;
    }

    method void printEnemySequence(int gameStep, int dinoPosition) {
        var int enemyIndex;
        var int i;
        var int enemyPosition;
        var int enemyValue;
        var int numOfVisibleEnemies;

        let i = 0;

        if (gameStep > 32) {
            let numOfVisibleEnemies = 32;
        }
        else {
            let numOfVisibleEnemies = gameStep;
        }

        while(i < numOfVisibleEnemies) {
            let enemyIndex = MathUtils.getCircularIndex(enemySequence, MathUtils.modulo(gameStep, enemySequenceLength) + numOfVisibleEnemies, -i);
            let enemyPosition = (11 * 32 *  (screenHeight / bitmapBoxSize)) + ((screenLength / bitmapBoxSize) -1 - i);

            let enemyValue = enemySequence[enemyIndex];

            if (enemyValue = 1) {
                do Graphics.drawCactus(enemyPosition);
            }

            if ((enemyValue = 0) & ~(enemyPosition = dinoPosition)) {
                do Graphics.drawBlank(enemyPosition);
            }

            let i = i + 1;
        }

        return;
    }

    method void drawTerrain() {
        var int position;
        var int i;

        let position = 12 * (screenHeight / bitmapBoxSize) * 32;

        let i = 0;
        while(i < (screenLength / bitmapBoxSize)) {
            do Graphics.drawTerrain1(position);
            let position = position + 1;

            do Graphics.drawTerrain2(position);
            let position = position + 1;

            do Graphics.drawTerrain3(position);
            let position = position + 1;

            let i = i + 1;
        }

        return;
    }

    method void dispose() {
        do Memory.deAlloc(this);
        return;
    }

}