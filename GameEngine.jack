class GameEngine {
    field Array enemySequence;
    field int enemySequenceLength;

    field int screenLength;
    field int screenHeight;
    field int bitmapBoxSize;

    field int numOfRows;
    field int numOfColumns;

    field int dinoXPositionOffset;
    field int dinoNoJumpYPosition;
    field int dinoJumpYPosition;

    field int delay;
    field int minimumDelay;
    field int delayDecrement;

    constructor GameEngine new() {
        return this;
    }

    method void initializeComponents() {
        let enemySequenceLength = 32;
        do initializeEnemySequence();

        let screenHeight = 256;
        let screenLength = 512;
        let bitmapBoxSize = 16;

        let numOfRows = screenHeight / bitmapBoxSize;
        let numOfColumns = screenLength / bitmapBoxSize;

        let dinoXPositionOffset = (numOfColumns / 2) - 1; // half of horizontal axis
        let dinoNoJumpYPosition = numOfRows * 11 * numOfColumns; // 11th row
        let dinoJumpYPosition = numOfRows * 10 * numOfColumns; // 10th row

        let delay = 300;
        let minimumDelay = 100;
        let delayDecrement = 15;

        return;
    }

    method void run() {
        var char key;
        var int currentEnemy;
        var int dinoPosition;
        var boolean clash;
        var int gameStep;
        var int currentEnemyIndex;
        var boolean jumped;
        var int jumpCountdown;
        var boolean dinoJustLanded;
        var boolean dinoJustJumped;
        var int currentEnemyOffset;

        let clash = false;
        let gameStep = 0;

        do initializeComponents();
        do drawTerrain();

        while(~clash) {

            // decrement jump duration
            if (jumped & (jumpCountdown > 0)) {
                let jumpCountdown = jumpCountdown - 1;
            }

            // just landed
            if (jumpCountdown = 0) {
                let jumped = false;
                let dinoJustLanded = true;
            }

            // detect just jumped
            let key = Keyboard.keyPressed();
            if ((key = 131) & ~jumped) { // up arrow
                let jumped = true;
                let jumpCountdown = 4;
                let dinoJustJumped = true;
            }

            if (jumped) {
                let dinoPosition = dinoJumpYPosition + dinoXPositionOffset;
            }
            else {
                let dinoPosition = dinoNoJumpYPosition + dinoXPositionOffset;
            }

            if (dinoJustLanded) {
                do Graphics.drawBlank(dinoJumpYPosition + dinoXPositionOffset);
            }

            if (dinoJustJumped) {
                do Graphics.drawBlank(dinoNoJumpYPosition + dinoXPositionOffset);
            }

            do Graphics.drawDino(dinoPosition);
            let clash = processEnemySequence(gameStep, dinoPosition);

            // prepare next stage
            let dinoJustLanded = false;
            let dinoJustJumped = false;

            if (MathUtils.modulo(gameStep, 2 * enemySequenceLength) = 0) {
                let delay = delay - delayDecrement;
                if (delay < minimumDelay) {
                    let delay = minimumDelay;
                }
            }

            let gameStep = gameStep + 1;
            do Sys.wait(delay);
        }

        // Game Over
        
        do Output.println(); do Output.println(); do Output.println(); do Output.println();
        do Output.printString("    *** G A M E  O V E R ***    ");
        do Output.printString("        Your score is: ");
        do Output.printInt(gameStep);

        return;
    }

    method void initializeEnemySequence() {
        let enemySequence = Array.new(enemySequenceLength);
        let enemySequence[0] = 1; // cactus
        let enemySequence[1] = 0;
        let enemySequence[2] = 0;
        let enemySequence[3] = 0;
        let enemySequence[4] = 0;
        let enemySequence[5] = 0;
        let enemySequence[6] = 0;
        let enemySequence[7] = 1; // cactus
        let enemySequence[8] = 0;
        let enemySequence[9] = 0;
        let enemySequence[10] = 0;
        let enemySequence[11] = 0;
        let enemySequence[12] = 0;
        let enemySequence[13] = 0;
        let enemySequence[14] = 1; // cactus
        let enemySequence[15] = 0;
        let enemySequence[16] = 0;
        let enemySequence[17] = 0;
        let enemySequence[18] = 0;
        let enemySequence[19] = 0;
        let enemySequence[20] = 0;
        let enemySequence[21] = 0;
        let enemySequence[22] = 0;
        let enemySequence[23] = 0;
        let enemySequence[24] = 1; // cactus
        let enemySequence[25] = 0;
        let enemySequence[26] = 0;
        let enemySequence[27] = 0;
        let enemySequence[28] = 0;
        let enemySequence[29] = 0;
        let enemySequence[30] = 0;
        let enemySequence[31] = 0;
        return;
    }

    method boolean processEnemySequence(int gameStep, int dinoPosition) {
        // Prints the current enemy sequence and detect the collision
        var int enemyIndex;
        var int i;
        var int enemyPosition;
        var int enemyValue;
        var int numOfVisibleEnemies;
        var boolean clash;

        let i = 0;
        let clash = false;

        if (gameStep > numOfColumns) {
            let numOfVisibleEnemies = numOfColumns;
        }
        else {
            let numOfVisibleEnemies = gameStep;
        }

        while((i < numOfVisibleEnemies) & ~clash) {
            let enemyIndex = MathUtils.getCircularIndex(enemySequenceLength, MathUtils.modulo(gameStep, enemySequenceLength), -i);
            let enemyValue = enemySequence[enemyIndex];
            let enemyPosition = (11 * numOfColumns *  numOfRows) + (numOfColumns -1 - i);

            if (enemyValue = 1) {
                do Graphics.drawCactus(enemyPosition);
            }

            if ((enemyValue = 0) & ~(enemyPosition = dinoPosition)) {
                do Graphics.drawBlank(enemyPosition);
            }

            if ((enemyValue = 1) & (enemyPosition = dinoPosition)) {
                let clash = true;
            }

            let i = i + 1;
        }

        return clash;
    }

    method void drawTerrain() {
        var int position;
        var int i;

        let position = 12 * numOfRows * numOfColumns;

        let i = 0;
        while(i < numOfColumns) {
            do Graphics.drawTerrain1(position);
            let position = position + 1;

            do Graphics.drawTerrain2(position);
            let position = position + 1;

            do Graphics.drawTerrain3(position);
            let position = position + 1;

            let i = i + 1;
        }

        return;
    }

    method void dispose() {
        do Memory.deAlloc(this);
        return;
    }
}